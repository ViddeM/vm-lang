use crate::core_types::{Program, Statement, Identifier, Expression, ComparisonOperator, Type, Argument, Function, UnaryOperator};

grammar;

match {
    "fn" => FN,
    "let" => LET,
    r"false" => FALSE,
    r"true" => TRUE,
    r"while" => WHILE,
    r"if" => IF,
    r"else" => ELSE,
    r"int" => INT,
    r"bool" => BOOL,
    r"String" => STRING,
    r"return" => RETURN,
    r"for" => FOR,
    "(",
    ")",
    "{",
    "}",
    "==",
    "!=",
    "<=",
    ">=",
    "<",
    ">",
    "=",
    "++",
    "--",
    "+",
    "-",
    "*",
    "/",
    "!",
    ",",
    ";",
    ":",
    "",
    r"[-]?\d+" => INTEGER,
    r#""[^"]*""# => STR,
    r"\s*" => { }, // The default whitespace skipping is disabled an `ignore pattern` is specified
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
} else {
    r"[a-zA-Z](([a-zA-Z0-9]|-|_)*([a-zA-Z0-9]))*" => ID,
}

pub Prgr: Program = {
    <funcs: Func*> => Program { functions: funcs },
}

Func: Function = {
    FN <id: Ident> "(" <args: Comma<Arg>> ")" ":" <ret: Type> "{" <stmts: Stmt*> "}" => Function {
        name: id,
        arguments: args,
        statements: stmts,
        return_type: ret,
    },
    FN <id: Ident> "(" <args: Comma<Arg>> ")" "{" <stmts: Stmt*> "}" => Function {
        name: id,
        arguments: args,
        statements: stmts,
        return_type: Type::Void,
    }
}

Arg: Argument = {
    <id: Ident> ":" <tp: Type> => Argument { name: id, t: tp },
}

Stmt: Statement = {
    LET <id: Ident> "=" <expr: Expr> ";" => Statement::Let(id, expr),
    WHILE <expr: Expr> <b: Block> => Statement::While(expr, Box::new(b)),
    IF <expr: Expr> <b: Block> => Statement::If(expr, Box::new(b)),
    IF <expr: Expr> <if_block: Block> ELSE <else_block: Block> => Statement::IfElse(expr, Box::new(if_block), Box::new(else_block)),
    RETURN <expr: Expr> ";" => Statement::Return(Some(expr)),
    RETURN ";" => Statement::Return(None),
    <expr: Expr> ";" => Statement::Expression(expr),
}

Block: Statement = {
    "{" <stmts: Stmt*> "}" => Statement::Block(stmts),
}

Expr: Expression = {
    Assignment,
}

Assignment: Expression = {
    <var: Ident> "=" <expr: Assignment> => Expression::Assignment(var, Box::new(expr)),
    Comparison,
}

Comparison: Expression = {
    <a: Term> <op: CmpOp> <b: Term> => Expression::Comparison(Box::new(a), Box::new(b), op),
    Term,
}

Term: Expression = {
    <expa: Term> "+" <expb: Factor> => Expression::Plus(Box::new(expa), Box::new(expb)),
    <expa: Term> "-" <expb: Factor> => Expression::Minus(Box::new(expa), Box::new(expb)),
    Factor,
}

Factor: Expression = {
    <expa: Factor> "*" <expb: FunctionCall> => Expression::Times(Box::new(expa), Box::new(expb)),
    <expa: Factor> "/" <expb: FunctionCall> => Expression::Divide(Box::new(expa), Box::new(expb)),
    PreUnaryOperators,
}

PreUnaryOperators: Expression = {
    "!" <expr: PostUnaryOperators> => Expression::NotUnaryOperation(Box::new(expr)),
    "++" <var: Ident> => Expression::PreUnaryOperation(var, UnaryOperator::Inc),
    "--" <var: Ident> => Expression::PreUnaryOperation(var, UnaryOperator::Dec),
    PostUnaryOperators,
}

PostUnaryOperators: Expression = {
    <var: Ident> "++" => Expression::PostUnaryOperation(var, UnaryOperator::Inc),
    <var: Ident> "--" => Expression::PostUnaryOperation(var, UnaryOperator::Dec),
    FunctionCall,
}

FunctionCall: Expression = {
    <name: Ident> "(" <args: Comma<Expr>> ")" => Expression::FunctionCall(String::from(name), args),
    Literal,
}

Literal: Expression = {
    FALSE => Expression::BooleanLiteral(false),
    TRUE => Expression::BooleanLiteral(true),
    <lit: Str> => Expression::StringLiteral(lit),
    <num: INTEGER> => Expression::IntegerLiteral(num.parse::<i64>().unwrap()),
    <var: Ident> => Expression::Variable(var),
    "(" <expr: Expr> ")" => expr,
}

Str: String = {
    STR => String::from(<>.strip_prefix("\"").unwrap().strip_suffix("\"").unwrap())
}

Ident: Identifier = {
    <id: ID> => Identifier::from(id)
}

// Generic helper-rule for comma-separated lists (taken from github.com/algebraicdb/algebraicdb)
Comma<E>: Vec<E> =
    <v0:(<E> ",")*> <e1:E?> =>
        v0.into_iter().chain(e1).collect();

CmpOp: ComparisonOperator = {
    "==" => ComparisonOperator::Equals,
    "!=" => ComparisonOperator::NotEquals,
    "<=" => ComparisonOperator::LessOrEqual,
    ">=" => ComparisonOperator::GreaterOrEqual,
    "<" => ComparisonOperator::LessThan,
    ">" => ComparisonOperator::GreaterThan,
}

Type: Type = {
    INT => Type::Integer,
    BOOL => Type::Boolean,
    STRING => Type::String,
}